plugins {
    id("com.github.spotbugs") version "4.7.1"
    id "java-library"
    id "maven-publish"
    id "checkstyle"
    id "signing"
    id "jacoco"
    id "pmd"
}

description = "Reified Generic"
group = "ninja.javahacker"
version = "1.1.0-2021.05.03"

def artifactName = "reified-generic"
def moduleName = "ninja.javahacker.reifiedgeneric"

def versionJavaCompiler = "9"
def doclint = true

def versionApiguardian = "1.1.1"
def versionCheckstyle = "8.42"
def versionSbContrib = "7.4.7"
def versionFindSecBugs = "1.11.0"
def versionJaCoCo = "0.8.6"
def versionJcip = "1.0"
def versionJunit = "5.8.0-M1"
def versionLombok = "1.18.20"
def versionPmd = "6.34.0"
def versionSpotBugs = "4.2.3"

dependencies {

    // JUnit
    testImplementation(group: "org.junit.jupiter", name: "junit-jupiter-api", version: versionJunit)
    testRuntimeOnly(group: "org.junit.jupiter", name: "junit-jupiter-engine", version: versionJunit)
    testRuntimeOnly(group: "org.junit.jupiter", name: "junit-jupiter-params", version: versionJunit)
    testCompileOnly(group: "org.apiguardian", name: "apiguardian-api", version: versionApiguardian)

    // Lombok
    if (versionLombok != "edge") {
        annotationProcessor(group: "org.projectlombok", name: "lombok", version: versionLombok)
        compileOnly(group: "org.projectlombok", name: "lombok", version: versionLombok)
        testAnnotationProcessor(group: "org.projectlombok", name: "lombok", version: versionLombok)
        testCompileOnly(group: "org.projectlombok", name: "lombok", version: versionLombok)
    } else {
        annotationProcessor(files("libs/lombok-edge.jar"))
        compileOnly(files("libs/lombok-edge.jar"))
        testAnnotationProcessor(files("libs/lombok-edge.jar"))
        testCompileOnly(files("libs/lombok-edge.jar"))
    }

    // SpotBugs + plugins
    spotbugs(group: "com.github.spotbugs", name: "spotbugs", version: versionSpotBugs)
    spotbugs(configurations.spotbugsPlugins.dependencies)
    api(group: "net.jcip", name: "jcip-annotations", version: versionJcip)
    api(group: "com.github.spotbugs", name: "spotbugs-annotations", version: versionSpotBugs)
    spotbugsPlugins(group: "com.h3xstream.findsecbugs", name: "findsecbugs-plugin", version: versionFindSecBugs)
    spotbugsPlugins(group: "com.mebigfatguy.sb-contrib", name: "sb-contrib", version: versionSbContrib)
}

// Do not change anything below here.

println("Using Java " + org.gradle.api.JavaVersion.current() + ".")

repositories {
    flatDir {
        dirs("libs")
    }
    mavenLocal()
    mavenCentral()
}

tasks.withType(JavaCompile) {
    def extraCompilerArgs = [
        "--add-opens", "jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED",
        "--add-opens", "jdk.compiler/com.sun.tools.javac.comp=ALL-UNNAMED",
        "--add-opens", "jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED",
        "--add-opens", "jdk.compiler/com.sun.tools.javac.main=ALL-UNNAMED",
        "--add-opens", "jdk.compiler/com.sun.tools.javac.model=ALL-UNNAMED",
        "--add-opens", "jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED",
        "--add-opens", "jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED",
        "--add-opens", "jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED",
        "--add-opens", "jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED"
    ]
    options.encoding = "UTF-8"
    options.debug = true
    options.fork = true
    options.compilerArgs << "-parameters" << "-Xlint:all,-processing"
    options.compilerArgs.addAll(extraCompilerArgs)
}

compileJava {
    sourceCompatibility = versionJavaCompiler
    targetCompatibility = versionJavaCompiler
    if (doclint) {
        options.compilerArgs << "-Xdoclint:all/protected"
    } else {
        options.compilerArgs << "-Xdoclint:none"
    }
}

compileTestJava {
    sourceCompatibility = org.gradle.api.JavaVersion.current()
    targetCompatibility = org.gradle.api.JavaVersion.current()
    options.compilerArgs << "-Xdoclint:none"
}

task delombok {
    description = "Delomboks the source code."
    configurations.compileOnly.setCanBeResolved(true)
    configurations.implementation.setCanBeResolved(true)
    def delombokPath = "${configurations.compileOnly.asPath};${configurations.implementation.asPath}"
    doLast {
        ant.mkdir(dir: "build/src-delomboked")
        ant.taskdef(name: "delombok", classname: "lombok.delombok.ant.Tasks\$Delombok", classpath: delombokPath)
        ant.delombok(verbose: "true", encoding: "UTF-8", to: "build/src-delomboked", from: "src/main/java", classpath: delombokPath)
    }
}

tasks.withType(Javadoc) {
    dependsOn(delombok)
    source = ["build/src-delomboked"]
    options.encoding = "UTF-8"
    options.charSet = "UTF-8"
    options.docEncoding = "UTF-8"
    options.docTitle = "${project.name} API"
    options.windowTitle = "${project.name} API"
    options.header = "<b>${project.name}</b>"
    options.addBooleanOption("html5", true)
    options.tags = ["apiNote:a:API Note:", "implSpec:a:Implementation Requirements:", "implNote:a:Implementation Note:"]
}

checkstyleMain {
    configFile = rootProject.file("${rootDir}/config/checkstyle/main.xml")
}

checkstyleTest {
    configFile = rootProject.file("${rootDir}/config/checkstyle/test.xml")
}

checkstyle {
    toolVersion = versionCheckstyle
    configProperties = [
        "checkstyle.cache.file": "${buildDir}/checkstyle.cache",
    ]
    ignoreFailures = true
    showViolations = true
}

pmd {
    toolVersion = versionPmd
    ignoreFailures = true
}

spotbugs {
    toolVersion = versionSpotBugs
    effort = "max"
    reportLevel = "low"
    omitVisitors = ["WeakExceptionMessaging", "OverlyPermissiveMethod"]
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

jar {
    archiveBaseName = artifactName
    duplicatesStrategy = "exclude"
    inputs.property("moduleName", moduleName)
    manifest {
        attributes("Automatic-Module-Name": moduleName)
    }
}

task sourcesJar(type: Jar) {
    from(sourceSets.main.allSource)
    classifier = "sources"
}

task javadocJar(type: Jar) {
    from(javadoc)
    classifier = "javadoc"
}

test {
    useJUnitPlatform()
    finalizedBy(jacocoTestReport)
}

jacoco {
    toolVersion = versionJaCoCo
}

jacocoTestReport {
    dependsOn(test)
    reports {
        xml.enabled = false
        csv.enabled = false
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = artifactName
            from(components.java)
            artifact(sourcesJar)
            artifact(javadocJar)
            pom {
                developers {
                    developer {
                        name = "Victor Williams Stafusa da Silva"
                        email = "victorwssilva@gmail.com"
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

spotbugsTest.enabled = false
pmdTest.enabled = false